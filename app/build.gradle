apply plugin: "com.android.application"

apply from: "$rootDir/config/dependencies/android_dependencies.gradle"
apply from: "$rootDir/config/dependencies/compose_dependencies.gradle"
apply from: "$rootDir/config/artifacts.gradle"

apply plugin: "kotlin-kapt"
apply plugin: "kotlin-parcelize"

android {
    defaultConfig {
        applicationId "com.escodro.alkaa"
        versionCode Releases.versionCode
        versionName Releases.versionName
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warningsAsErrors true
        abortOnError false // Google needs to update their libraries to API Level 10.
        htmlReport true
        checkDependencies true

        lintConfig file("${rootDir}/config/filters/lint.xml")
        htmlOutput file("${buildDir}/reports/lint.html")
    }

    kapt {
        javacOptions {
            option("-Xmaxerrs", 1000)
            option("-Werror")
        }
    }

    dynamicFeatures = [":features:tracker"]
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

}

dependencies {
    implementation project(":libraries:core")
    implementation project(":libraries:splitInstall")
    implementation project(':libraries:designsystem')
    implementation project(":libraries:navigation")
    implementation project(":data:local")
    implementation project(":data:repository")
    implementation project(":domain")
    implementation project(":features:task")
    implementation project(":features:alarm-api")
    implementation project(":features:alarm")
    implementation project(":features:category-api")
    implementation project(":features:category")
    implementation project(":features:preference")
    implementation project(":features:search")

    implementation Deps.compose.navigation
    implementation Deps.compose.activity
    implementation Deps.android.playCore
    implementation Deps.koin.android
}
