apply plugin: "com.android.application"

apply from: "$rootDir/config/dependencies/android_dependencies.gradle"
apply from: "$rootDir/config/dependencies/compose_dependencies.gradle"
apply from: "$rootDir/config/artifacts.gradle"

apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs"

android {
    defaultConfig {
        applicationId "com.escodro.alkaa"
        versionCode Releases.versionCode
        versionName Releases.versionName
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        htmlReport true
        checkDependencies true

        lintConfig file("${rootDir}/config/filters/lint.xml")
        htmlOutput file("${buildDir}/reports/lint.html")
    }

    androidExtensions {
        experimental = true
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.compose
        kotlinCompilerVersion Versions.kotlin
    }

    kapt {
        javacOptions {
            option("-Xmaxerrs", 1000)
            option("-Werror")
        }
    }

    dynamicFeatures = [":features:tracker"]
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

}

dependencies {
    implementation project(":libraries:core")
    implementation project(":libraries:navigation")
    implementation project(":libraries:splitInstall")
    implementation project(":libraries:theme")
    implementation project(":data:local")
    implementation project(":data:repository")
    implementation project(":data:domain")
    implementation project(":features:task")
    implementation project(":features:alarm")
    implementation project(":features:category")
    implementation project(":features:preference")
    implementation project(":features:search")

    implementation Deps.android.preference
    implementation Deps.android.constraintLayout
    implementation Deps.android.ktx
    implementation Deps.android.room.runtime
    implementation Deps.android.navigation.fragment
    implementation Deps.android.navigation.ui
    implementation Deps.android.lifecycle.viewmodel
    implementation Deps.android.playCore

    implementation Deps.timber

    implementation Deps.koin.viewModel
    androidTestImplementation Deps.koin.test

    implementation Deps.coroutines.test

    testImplementation Deps.test.junit
    androidTestImplementation Deps.test.runner
    androidTestImplementation Deps.test.rules
    androidTestImplementation Deps.test.core
    androidTestImplementation Deps.test.espresso.core
    androidTestImplementation Deps.test.espresso.intents
    androidTestImplementation Deps.test.espresso.contrib
    androidTestImplementation Deps.test.junitExt
    androidTestImplementation Deps.test.uiAutomator
    androidTestImplementation Deps.compose.uiTest
    androidTestImplementation Deps.compose.junit4
}
